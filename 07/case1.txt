% A has orthonormal columns
%-----------------------------------------------------------------------

fof(left_identity, axiom,
	! [A] : mult(i, A) = A).
	
fof(right_identity, axiom,
	! [A] : mult(A, i) = A).
	
fof(associativity, axiom,
	! [A,B,C] : mult(mult(A, B), C) = mult(A, mult(B, C))).
	
fof(adjoint_twice, axiom,
	! [A] : adjoint(adjoint(A)) = A).
	
fof(adjoint_mult, axiom, 
	! [A,B] : adjoint(mult(A, B)) = mult(adjoint(B), adjoint(A))).
	
fof(mpi, axiom,
	! [A,X] : (mpi(A, X) <=>
		A = mult(A, mult(X, A)) &
		X = mult(X, mult(A, X)) &
		adjoint(mult(A, X)) = mult(A, X) &
		adjoint(mult(X, A)) = mult(X, A))).
			
%-----------------------------------------------------------------------

fof(theorem, conjecture,
	! [A] : (
		mult(adjoint(A), A) = i
		=>
		mpi(A, adjoint(A))
	)
).
