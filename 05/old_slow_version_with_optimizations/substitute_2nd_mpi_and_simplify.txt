% i*A = A
fof(left_identity, axiom,
	! [A] : mult(i, A) = A).
% A*i = A
fof(right_identity, axiom,
	! [A] : mult(A, i) = A).

% (AB)C = A(BC)
fof(associativity, axiom,
	! [A,B,C] : mult(mult(A, B), C) = mult(A, mult(B, C))).

% (A*)* = A
fof(adjoint_twice, axiom,
	! [A] : adjoint(adjoint(A)) = A).

% (AB)* = B*A*
fof(adjoint_mult, axiom, 
	! [A,B] : adjoint(mult(A, B)) = mult(adjoint(B), adjoint(A))).

% Moore-Penrose Inverse: AXA = A, XAX = X, (AX)* = AX, (XA)* = XA
fof(mpi, axiom,
	! [A,X] : (mpi(A, X) <=>
		A = mult(A, mult(X, A)) &
		X = mult(X, mult(A, X)) &
		mult(adjoint(X), adjoint(A)) = mult(A, X) &
		mult(adjoint(X), adjoint(A)) = mult(X, A))).

%-----------------------------------------------------------------------

fof(unitary, axiom,
	! [A] : (unitary(A) <=> mult(A, adjoint(A)) = i & mult(adjoint(A), A) = i)).

fof(theorem6, conjecture,
	! [A, U, S, V, St] : (
		A = mult(U, mult(S, V)) & 
		unitary(U) &
		unitary(V) & 
		mpi(S, St) 
		=>
		A = mult(A, mult(mult(adjoint(V), mult(St, adjoint(U))), A)) &
		mult(adjoint(V), mult(St, adjoint(U))) = mult(mult(adjoint(V), mult(St, adjoint(U))), mult(A, mult(adjoint(V), mult(St, adjoint(U))))) &
		mult(adjoint(mult(adjoint(V), mult(St, adjoint(U)))), adjoint(A)) = mult(A, mult(adjoint(V), mult(St, adjoint(U)))) &
		mult(adjoint(A), adjoint(mult(adjoint(V), mult(St, adjoint(U))))) = mult(mult(adjoint(V), mult(St, adjoint(U))), A)
	)
).
